ext {
    versions = [
            koin: "1.0.0"
    ]
}

def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file("signing.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (FileNotFoundException ignored) {
    println 'signing.properties file was not found, no release builds will be available'
    // Put dummy values so that the debug builds work
    keystoreProperties['storeFile'] = rootProject.file('build.gradle') // bogus file, I know
    keystoreProperties['keyAlias'] = ''
    keystoreProperties['storePassword'] = ''
}

apply from: 'versioning.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 30
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
    defaultConfig {
        applicationId "io.github.benoitduffez.cupsprint"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode buildVersionCode()
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }
    flavorDimensions "type"
    productFlavors {
        fdroid {
            dimension "type"
            ext.useFabric = false
        }
        playstore {
            dimension "type"
            ext.useFabric = true
        }
    }
    namespace 'io.github.benoitduffez.cupsprint'
}

dependencies {
    implementation 'javax.jmdns:jmdns:3.4.1'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Koin DI
    implementation "org.koin:koin-core:${versions.koin}"
    implementation "org.koin:koin-core-ext:${versions.koin}"
    implementation "org.koin:koin-android:${versions.koin}"
    apply plugin: 'kotlin-android-extensions'
}

apply plugin: 'jacoco'
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugTree]))
    getExecutionData().setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}

play {
    def envTrack = System.getenv('PLAY_STORE_RELEASE_CHANNEL')
    if ("production" == envTrack || "beta" == envTrack || "alpha" == envTrack || "rollout" == envTrack) {
        track = envTrack
    } else {
        println "The PLAY_STORE_RELEASE_CHANNEL environment variable is invalid, it was ignored"
    }
    jsonFile = rootProject.file('GooglePlayAPI.json')
}

android.productFlavors.each {
    flavor -> if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains(flavor.name) && flavor.ext.useFabric){
        apply plugin: 'io.fabric'
    }
}
